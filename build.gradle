/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"
apply plugin: 'java'

group = 'com.thoughtworks.gocd'

gocdPlugin {
  id = 'com.thoughtworks.gocd.elastic-agent.ecs'
  pluginVersion = '7.1.0'
  goCdVersion = '20.9.0'
  name = 'GoCD Elastic Agent Plugin for Amazon ECS'
  description = 'GoCD Elastic Agent Plugin for Amazon Elastic Container Service allow for more efficient use of instances'
  vendorName = 'ThoughtWorks, Inc.'
  vendorUrl = 'https://github.com/gocd/gocd-ecs-elastic-agent'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'gocd-ecs-elastic-agent'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.findByName('jar')]
}

version = gocdPlugin.fullVersion(project)

repositories {
  mavenCentral()
  mavenLocal()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '22.1.0'
  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.4'

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
  implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'joda-time', name: 'joda-time', version: '2.10.13'
  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

  implementation group: 'com.amazonaws', name: 'aws-java-sdk-ecs', version: '1.12.186'
  implementation group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: '1.12.181'
  implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.31'

  modules {
    module('commons-logging:commons-logging') {
      replacedBy('org.slf4j:jcl-over-slf4j', "Everything should go via SLF4J")
    }
  }
  implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.36'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.2'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'

  testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: '21.4.0'
  testImplementation group: 'uk.org.webcompere', name: 'system-stubs-jupiter', version: '2.0.1'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.22.0'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.4.0'
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
  testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.14.3'
}

tasks.withType(JavaCompile) {
  options.deprecation = true
  options.encoding = 'utf-8'
  options.warnings = true
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-serial"
}

test {
  useJUnitPlatform()
}

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}

task bundleInstall {
  inputs.files(project.files('contrib/cloudformation-template/Gemfile', 'contrib/cloudformation-template/Gemfile.lock'))
  outputs.dir(project.file('.bundle'))

  doLast {
    project.exec {
      commandLine = ['bundle']
      workingDir = project.file('contrib/cloudformation-template')
      standardOutput = System.out
      errorOutput = System.err
    }
  }
}

task stackfile {
  dependsOn bundleInstall

  inputs.file(project.file('contrib/cloudformation-template/ecs_cloud_formation_template.rb'))
  outputs.file(project.file("${project.buildDir}/ecs_cloud_formation_template.json"))

  doLast {
    project.exec {
      commandLine = ['bundle', 'exec', 'ruby', 'ecs_cloud_formation_template.rb', 'expand']
      workingDir = project.file('contrib/cloudformation-template')
      standardOutput = new FileOutputStream(project.file("${project.buildDir}/ecs_cloud_formation_template.json"))
      errorOutput = System.err
    }
  }
}
