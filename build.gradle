/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"
apply plugin: 'java'

group = 'com.thoughtworks.gocd'

gocdPlugin {
  id = 'com.thoughtworks.gocd.elastic-agent.ecs'
  pluginVersion = '7.3.0'
  goCdVersion = '20.9.0'
  name = 'GoCD Elastic Agent Plugin for Amazon ECS'
  description = 'GoCD Elastic Agent Plugin for Amazon Elastic Container Service allow for more efficient use of instances'
  vendorName = 'Thoughtworks, Inc.'
  vendorUrl = 'https://github.com/gocd/gocd-ecs-elastic-agent'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'gocd-ecs-elastic-agent'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.findByName('jar')]
}

version = gocdPlugin.fullVersion(project)

repositories {
  mavenCentral()
  mavenLocal()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

ext {
  deps = [
    gocdPluginApi: 'cd.go.plugin:go-plugin-api:23.5.0',
    awsSdk       : 'com.amazonaws:aws-java-sdk:1.12.636',
    lombok       : 'org.projectlombok:lombok:1.18.30',
  ]

  versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
  annotationProcessor project.deps.lombok

  compileOnly project.deps.lombok
  compileOnly project.deps.gocdPluginApi

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
  implementation(group: 'com.google.guava', name: 'guava', version: '33.0.0-jre') {
    // see https://github.com/google/guava/pull/6606
    exclude(module: 'listenablefuture')
    exclude(module: 'error_prone_annotations')
    exclude(module: 'checker-qual')
    exclude(module: 'jsr305')
  }
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
  implementation group: 'joda-time', name: 'joda-time', version: '2.12.5'
  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

  implementation group: 'com.amazonaws', name: 'aws-java-sdk-ecs', version: project.versions.awsSdk
  implementation group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: project.versions.awsSdk
  implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.32'

  modules {
    module('commons-logging:commons-logging') {
      replacedBy('org.slf4j:jcl-over-slf4j', "Everything should go via SLF4J")
    }
  }
  implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '2.0.10'

  testImplementation platform('org.junit:junit-bom:5.10.1')
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'

  testImplementation project.deps.gocdPluginApi
  testImplementation group: 'uk.org.webcompere', name: 'system-stubs-jupiter', version: '2.1.5'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.25.1'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.8.0'
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
  testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.17.2'
}

tasks.withType(JavaCompile) {
  options.deprecation = true
  options.encoding = 'utf-8'
  options.warnings = true
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-serial"
}

test {
  useJUnitPlatform()
}

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}

task bundleInstall {
  inputs.files(project.files('contrib/cloudformation-template/Gemfile', 'contrib/cloudformation-template/Gemfile.lock'))
  outputs.dir(project.file('.bundle'))

  doLast {
    project.exec {
      commandLine = ['bundle']
      workingDir = project.file('contrib/cloudformation-template')
      standardOutput = System.out
      errorOutput = System.err
    }
  }
}

task stackfile {
  dependsOn bundleInstall

  inputs.file(project.file('contrib/cloudformation-template/ecs_cloud_formation_template.rb'))
  outputs.file(project.file("${project.buildDir}/ecs_cloud_formation_template.json"))

  doLast {
    project.exec {
      commandLine = ['bundle', 'exec', 'ruby', 'ecs_cloud_formation_template.rb', 'expand']
      workingDir = project.file('contrib/cloudformation-template')
      standardOutput = new FileOutputStream(project.file("${project.buildDir}/ecs_cloud_formation_template.json"))
      errorOutput = System.err
    }
  }
}
